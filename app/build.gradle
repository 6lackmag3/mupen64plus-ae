apply plugin: 'com.android.application'
apply plugin: 'signing'
apply from: '../build_common/version_common.gradle'

android {

    allprojects {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }

        repositories {
            mavenCentral()
            google()
            maven {
                allowInsecureProtocol true
                url 'http://4thline.org/m2'
            }
        }
    }

    buildFeatures.viewBinding = true

    lintOptions {
        abortOnError false
        checkReleaseBuilds = false
    }
    defaultConfig {
        applicationId = "org.mupen64plusae.v3.alpha"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            minifyEnabled true
            jniDebuggable true
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
		
        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def builtType = variant.buildType.name
                outputFileName = "Mupen64PlusAE-${builtType}.apk"
            }
        }
    }

    bundle {
        language {
            enableSplit false
        }
    }

    defaultConfig {
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    sourceSets {
        main {
            debug {
                jniLibs.srcDirs '../ndkLibs/libs/debug'
            }
            release {
                jniLibs.srcDirs '../ndkLibs/libs/release'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/beans.xml'
        doNotStrip "*/armeabi/*.so"
        doNotStrip "*/armeabi-v7a/*.so"
        doNotStrip "*/x86/*.so"
        doNotStrip "*/x86_64/*.so"
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'org.tukaani:xz:1.9'
    implementation 'org.fourthline.cling:cling-core:2.1.2'
    implementation 'org.fourthline.cling:cling-support:2.1.2'

    // Other dependencies required by Cling, don't update the versions here
    // since cling requires the specific older versions to work correctly
    implementation 'org.eclipse.jetty:jetty-server:8.2.0.v20160908'
    implementation 'org.eclipse.jetty:jetty-servlet:8.2.0.v20160908'
    implementation 'org.eclipse.jetty:jetty-client:8.2.0.v20160908'
    implementation 'com.sun.net.httpserver:http:20070405'
    implementation 'org.slf4j:slf4j-jdk14:1.7.14'

    implementation ('net.java.dev.jna:jna-platform:5.10.0') {
        exclude group: 'net.java.dev.jna', module: 'jna'
    }
    implementation 'net.java.dev.jna:jna:5.10.0@aar'
    testImplementation 'junit:junit:4.13.2'
    implementation project(':ae-bridge')
    implementation project(':miniupnp-bridge')
    implementation project(':mupen64plus-audio-android')
    implementation project(':mupen64plus-video-gliden64')
    implementation project(':mupen64plus-video-glide64mk2')
    implementation project(':mupen64plus-video-gln64')
    implementation project(':mupen64plus-rsp-parallel')
    implementation project(':mupen64plus-rsp-cxd4')
    implementation project(':mupen64plus-rsp-hle')
    implementation project(':mupen64plus-input-android')
    implementation project(':mupen64plus-input-raphnet')
    implementation project(':mupen64plus-video-rice')
    implementation project(':mupen64plus-video-angrylion-plus')
    implementation project(':mupen64plus-video-parallel')
    implementation project(':mupen64plus-core')
}

